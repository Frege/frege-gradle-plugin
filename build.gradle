
apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "signing"

defaultTasks "build"

ext {
    isSnapshot = true
    baseVersion = "0.1"
    snapshotAppendix = "-SNAPSHOT"
    pluginVersion = baseVersion + (isSnapshot ? snapshotAppendix : "")

    sonatypeBaseUrl = "https://oss.sonatype.org"
    sonatypeSnapshotUrl = "$sonatypeBaseUrl/content/repositories/snapshots/"
    sonatypeRepositoryUrl = "$sonatypeBaseUrl/content/groups/public"
    sonatypeReleaseUrl = "$sonatypeBaseUrl/service/local/staging/deploy/maven2/"
    sonatypeUploadUrl = isSnapshot ? sonatypeSnapshotUrl : sonatypeReleaseUrl

    projectUrl = "https://github.com/mperry/gradle-frege-plugin"
    projectName = "GradleFregePlugin"
    pomProjectName = "GradleFregePlugin"
    baseJarName = "gradle-frege-plugin"

    groupName = "com.github.mperry.frege"
    scmUrl = "git://github.com/mperry/gradle-frege-plugin.git"
    scmGitFile = "scm:git@github.com:mperry/gradle-frege-plugin.git"
    projectDescription = "Frege gradle plugin"

    licenseName = "The Apache Software License, Version 2.0"
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    organisation = groupName

    primaryEmail = "frege-programming-language@googlegroups.com"

    fregeVersion = "3.22.324"
    fregeClassifier = "-g630677b"
}

repositories {

//  flatDir name:"frege-lib", dirs:"lib"
  mavenCentral()


}

version = pluginVersion
group = groupName

dependencies {
    compile "com.theoryinpractise.frege:frege:${fregeVersion}${fregeClassifier}"
    compile gradleApi()
    compile localGroovy()
}

Boolean doSigning() {
  signingEnabled.trim() == "true"
}



uploadArchives {
  enabled = true
  repositories {
    mavenDeployer {
      if (doSigning()) {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      }

      repository(url: sonatypeUploadUrl) {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }
      pom {
        groupId = project.group
        project {
          name pomProjectName
          packaging 'jar'
          description projectDescription
          url projectUrl
          organization {
            name pomProjectName
            url projectUrl
          }
          scm {
            url scmUrl
          }
          licenses {
            license {
              name licenseName
              url licenseUrl
              distribution 'repo'
            }
          }
          developers {
            developer {
              email primaryEmail
            }
          }
        }
      }
    }
  }
}